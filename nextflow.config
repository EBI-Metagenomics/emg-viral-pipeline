manifest {
    mainScript = 'virify.nf'
    nextflowVersion = '>=20.00.0'
}

params {
    max_cores = Runtime.runtime.availableProcessors()
    cores = Runtime.runtime.availableProcessors().intdiv(4)
    memory = '12'
    help = false
    profile = false

    // inputs
    illumina = ''
    fasta = ''
    list = false

    // databases
    virsorter = false
    virfinder = false
    viphog = false
    ncbi = false
    checkv = false
    rvdb = false
    pvogs = false
    vogdb = false
    vpf = false
    imgvr = false
    pprmeta = false
    meta = false

    virome = false
    hmmextend = false
    blastextend = false
    chromomap = false
    balloon = false
    onlyannotate = false
    mashmap_len = '2000'
    mashmap = ''
    evalue = 0.01
    prop = 0.1
    taxthres = 0.6
    factor = "$projectDir/references/viphogs_cds_per_taxon_cummulative.csv"

    sankey = 25
    chunk = 10
    length = 1.5 // in kb, so 0.5 filters all contigs shorter 500 nt

    // development
    viphog_version = 'v3'
    meta_version = 'v4'

    // folder structure
    output = 'results'
    assemblydir = '00-assembly'
    virusdir = '01-viruses'
    prodigaldir = '02-prodigal'
    phanotatedir = '02-phanotate'
    hmmerdir = '03-hmmer'
    blastdir = '04-blast'
    plotdir = '05-plots'
    taxdir = '06-taxonomy'
    checkvdir = '07-checkv'
    finaldir = '08-final'

    // location for autodownload data like databases
    dbs = 'nextflow-autodownload-databases'

    // optional profile configurations, mostly necessary for HPC execution [lsf, slurm]
    workdir = 'work'
    singularity_cachedir = 'singularity'
}

profiles {

    //executors
    local {
        executor {
                name = "local"
               	cpus = params.max_cores
        } 
        workDir = params.workdir
        params.databases = params.dbs
        params.cloudProcess = false
        includeConfig 'nextflow/configs/local.config'
    }

    lsf {
        workDir = params.workdir
        params.databases = params.dbs
        executor {
            name = "lsf"
            queueSize = 200
        }        
        params.cloudProcess = true
        process.cache = "lenient"
        includeConfig 'nextflow/configs/node.config'
    }

    slurm {
        workDir = params.workdir
        params.databases = params.dbs
        executor {
            name = "slurm"
            queueSize = 200
        }        
        params.cloudProcess = true
        process.cache = "lenient"
        includeConfig 'nextflow/configs/node.config'
    }

    
    //engines
    docker { 
        docker { enabled = true }
        includeConfig 'nextflow/configs/container.config'
    }

    singularity {
        singularity { 
                enabled = true
                autoMounts = true
                cacheDir = params.singularity_cachedir
        }
        includeConfig 'nextflow/configs/container.config'
    }

    conda { 
        // not working right now due to missing conda package for PPR-Meta! 
        includeConfig 'nextflow/configs/conda.config'
    }


    //pre-merged profiles for direct usage
    standard { 
        executor {
                name = "local"
               	cpus = params.max_cores
        }
        workDir = params.workdir
        params.databases = params.dbs
        params.cloudProcess = false
        includeConfig 'nextflow/configs/local.config'
        docker { enabled = true }
        includeConfig 'nextflow/configs/container.config'
    }

    ebi {
  	    params.workdir = "/hps/nobackup/rdf/metagenomics/service-team/nextflow-workdir/virify/"
  	    params.databases = "/hps/nobackup/rdf/metagenomics/service-team/ref-dbs/virify/"
  	    params.singularity_cachedir = "/hps/nobackup/rdf/metagenomics/service-team/singularity-cache/"

        workDir = params.workdir
     
        params.cloudProcess = true
        process.cache = "lenient"
        includeConfig 'nextflow/configs/node.config'

        singularity { 
                enabled = true
                autoMounts = true
                cacheDir = params.singularity_cachedir
        }
        includeConfig 'nextflow/configs/container.config' 
    }

    // CONFIGURE YOUR PRIVATE CLOUD
    gcloud {
        workDir = params.workdir
        params.databases = 'gs://databases-matrice/virify/'
        bucketDir = 'gs://matrice/virify-work'

        process.executor = 'google-lifesciences'
 
        google {
            project = 'nextflow-auto-255816'
            zone = 'europe-west1-b'
            lifeSciences.preemptible = false
            lifeSciences.bootDiskSize = 20.GB
        }

        params.cloudProcess = true
        includeConfig 'nextflow/configs/node.config'

        docker { enabled = true }
        includeConfig 'nextflow/configs/container.config'

        // we need a docker also for basic functionalities in the cloud
        process {
            withLabel: noDocker { cpus = 4; memory = '8.0 GB'; container = 'nanozoo/template:3.8--ccd0653' } 
        }
    }
} 
